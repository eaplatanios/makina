group = 'makina'
project.buildDir = 'build'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.7.7'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'scala'
    apply plugin: 'com.google.protobuf'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    version = '0.1'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile 'org.scala-lang:scala-library:2.11.8'
        compile 'org.slf4j:slf4j-api:1.7.21'
        compile 'org.apache.logging.log4j:log4j-api:2.5'
        compile 'org.apache.logging.log4j:log4j-core:2.5'
        compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.5'
        compile 'com.typesafe.scala-logging:scala-logging_2.11:3.1.0'
        compile 'com.google.protobuf:protobuf-java:3.0.0-beta-3'
        compile 'commons-cli:commons-cli:1.3.1'
        compile 'mysql:mysql-connector-java:5.1.34'
        compile 'com.lmax:disruptor:3.3.0'
        testCompile 'junit:junit:4.11'
    }

    compileJava {
        options.compilerArgs << '-XDignore.symbol.file'
        options.fork = true
        options.forkOptions.executable = 'javac'
    }

    protobuf {
        protoc {
            artifact = 'com.google.protobuf:protoc:3.0.0-beta-3'
        }
        generatedFilesBaseDir = "$projectDir/src"
    }

    test {
        ignoreFailures = true
    }

    apply plugin: 'jacoco'

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    check.dependsOn jacocoTestReport

    apply plugin: 'application'

    run {
        applicationDefaultJvmArgs = ['-Xmx6G']
        mainClassName = System.getProperty('class')
        if (mainClassName == null)
            mainClassName = ""
        def arguments = System.getProperty('args')
        if (arguments != null)
            args arguments
    }
}
